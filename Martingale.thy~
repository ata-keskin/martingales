theory Martingale
  imports Filtration Banach_Conditional_Expectation
begin

locale stochastic_process = sigma_finite_measure M for M +
  fixes X :: "'t :: {second_countable_topology,linorder_topology} \<Rightarrow> 'a \<Rightarrow> 'b::{real_normed_vector, second_countable_topology}"
  assumes random_variable[measurable]: "\<And>i. X i \<in> borel_measurable M"

locale adapted_process = filtered_sigma_finite_measure M F + stochastic_process M X for M F X +
  assumes adapted[measurable]: "\<And>i. (X i) \<in> borel_measurable (F i)"

locale martingale = adapted_process M F X for M F X +
  assumes integrable: "\<And>i. integrable M (X i)"
      and martingale_property: "\<And>i j. i \<le> j \<Longrightarrow> AE \<xi> in M. X i \<xi> = cond_exp M (F i) (X j) \<xi>"

locale martingale_preorder = martingale M F X for M F and X :: "_ \<Rightarrow> _ \<Rightarrow> _ :: {second_countable_topology, preorder, real_normed_vector}"

locale submartingale = adapted_process M F "X :: _ \<Rightarrow> _ \<Rightarrow> _ :: {second_countable_topology, preorder, real_normed_vector}" for M F X +
  assumes integrable: "\<And>i. integrable M (X i)"
      and submartingale_property: "\<And>i j. i \<le> j \<Longrightarrow> AE \<xi> in M. X i \<xi> \<le> cond_exp M (F i) (X j) \<xi>"

locale supermartingale = adapted_process M F "X :: _ \<Rightarrow> _ \<Rightarrow> _ :: {second_countable_topology, preorder, real_normed_vector}" for M F X +
  assumes integrable: "\<And>i. integrable M (X i)"
      and supermartingale_property: "\<And>i j. i \<le> j \<Longrightarrow> AE \<xi> in M. X i \<xi> \<ge> cond_exp M (F i) (X j) \<xi>"

sublocale martingale_preorder \<subseteq> submartingale using martingale_property by (unfold_locales) (force simp add: integrable)+

sublocale martingale_preorder \<subseteq> supermartingale using martingale_property by (unfold_locales) (force simp add: integrable)+

lemma (in filtered_sigma_finite_measure) martingale_const:  
  fixes f :: "'a \<Rightarrow> 'b::{second_countable_topology, banach}"
  assumes "integrable M f" "f \<in> borel_measurable (F bot)"
  shows "martingale M F (\<lambda>_. f)"
proof (unfold_locales, goal_cases)
  case (1 i)
  then show ?case using assms by (simp add: borel_measurable_integrable)
next
  case (2 i)
  then show ?case using assms by (metis bot.extremum measurable_from_subalg sets_F_mono space_F subalgebra_def)
next
  case (3 i)
  then show ?case using assms by blast
next
  case (4 i j)
  then show ?case using cond_exp_F_meas[OF assms(1), THEN AE_symmetric] assms by (metis (mono_tags, lifting) borel_measurable_subalgebra bot_least filtration.sets_F_mono filtration_axioms space_F)
qed 

lemma (in martingale) martingale_set_integral:
  assumes "A \<in> F i" "i \<le> j"
  shows "set_lebesgue_integral M A (X i) = set_lebesgue_integral M A (X j)"
proof -
  have "\<integral>x \<in> A. X i x \<partial>M = \<integral>x \<in> A. cond_exp M (F i) (X j) x \<partial>M"
    using martingale_property[OF assms(2)]
    by (intro set_lebesgue_integral_cong_AE[OF subalgebra[THEN subsetD], OF assms(1) random_variable cond_exp.measurable, OF _ integrable])
       (auto simp add: subalgebra space_F subalgebra_def)
  also have "... = \<integral>x \<in> A. X j x \<partial>M" 
    using assms
    by (intro cond_exp.set_integral[OF _ integrable, symmetric]) (auto simp add: subalgebra space_F subalgebra_def)
  finally show ?thesis .
qed

lemma (in filtered_sigma_finite_measure) martingale_cond_exp:
  assumes "integrable M X"
  shows "martingale M F (\<lambda>i. cond_exp M (F i) X)"
proof (unfold_locales, goal_cases)
  case (1 i)
  then show ?case using assms by (intro borel_measurable_cond_exp, auto simp add: subalgebra space_F subalgebra_def)
next
  case (2 i)
  then show ?case using assms by (intro borel_measurable_cond_exp', auto simp add: subalgebra space_F subalgebra_def)
next
  case (3 i)
  then show ?case using assms by (intro integrable, auto simp add: subalgebra space_F subalgebra_def)
next
  case (4 i j)
  have "set_lebesgue_integral M A (cond_exp M (F j) X) = set_lebesgue_integral M A (cond_exp M (F i) X)" if "A \<in> F i" for A
    using cond_exp.set_integral[of "F i" X A] cond_exp.set_integral[of "F j" X A] assms that sets_F_mono[OF 4] 
    by (fastforce simp add: subalgebra space_F subalgebra_def)
  then show ?case by (intro AE_symmetric[OF cond_exp.characterization], (fastforce simp add: assms subalgebra space_F subalgebra_def intro!: cond_exp.integrable cond_exp.measurable')+)
qed

(* This does not work, why?

sublocale filtered_prob_space \<subseteq> martingale M F "(\<lambda>i. cond_exp M (F i) X)"
proof -
  show "martingale M F (\<lambda>i. cond_exp M (F i) X)" sorry
qed

*)
       
sublocale martingale \<subseteq> submartingale M F "X :: ( 'b \<Rightarrow> 'a \<Rightarrow> 'c :: {second_countable_topology, preorder, real_normed_vector})"

(* Martingales for finite measures *)

end